      ******************************************************************
      * AUTHOR: LAUT
      * DATE: 23/10/2024
      * Tectonics: cobc
      ******************************************************************
       IDENTIFICATION DIVISION.
      *****************************************************************
       PROGRAM-ID.     PTLAUT05.
      *****************************************************************
       ENVIRONMENT DIVISION.
      ******************************************************************
      *                    CONFIGURATION SECTION                       *
      ******************************************************************
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      ******************************************************************
      *                     INPUT-OUTPUT SECTION                       *
      ******************************************************************
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.
            SELECT ENTRADA ASSIGN ENTRADA
                   ORGANIZATION IS SEQUENTIAL
                   ACCESS MODE  IS SEQUENTIAL
                   FILE STATUS  IS FS-ENTRADA.
            SELECT FECLIQ ASSIGN FECLIQ
                   ORGANIZATION IS SEQUENTIAL
                   ACCESS MODE  IS SEQUENTIAL
                   FILE STATUS  IS FS-FECLIQ.
            SELECT SALIDA  ASSIGN SALIDA
                   ORGANIZATION IS SEQUENTIAL
                   ACCESS MODE  IS SEQUENTIAL
                   FILE STATUS  IS FS-SALIDA.
            SELECT DESCAR ASSIGN DESCAR
                   ORGANIZATION IS SEQUENTIAL
                   ACCESS MODE  IS SEQUENTIAL
                   FILE STATUS  IS FS-DESCAR.
      *
       I-O-CONTROL.
      ******************************************************************
      *                                                                *
      *                    D A T A   D I V I S I O N                   *
      *                                                                *
      ******************************************************************
       DATA DIVISION.
       FILE SECTION.
      *
        FD FECLIQ
            BLOCK CONTAINS 0 RECORDS
            DATA RECORD REG-FECLIQ.
        01  R-FECLIQ       PIC X(080).
      *
        FD  ENTRADA
            BLOCK CONTAINS 0 RECORDS
            DATA RECORD R-ENTRADA.
        01  R-ENTRADA      PIC X(3104).
      *
        FD  SALIDA
            BLOCK CONTAINS 0 RECORDS
            DATA RECORD R-SALIDA.
        01  R-SALIDA       PIC X(1000).
      *
        FD  DESCAR
            BLOCK CONTAINS 0 RECORDS
            DATA RECORD R-DESCAR.
        01  R-DESCAR       PIC X(3104).
      ******************************************************************
      *                    WORKING-STORAGE SECTION                     *
      ******************************************************************
       WORKING-STORAGE SECTION.
      *----------------------------------------------------------------*
      *              C A M P O S    A U X I L I A R E S                *
      *----------------------------------------------------------------*
       01 VARIABLES.
          05 WS-SQLCODZ                         PIC -ZZ9.
          05 WS-DESCARTAR                       PIC X(2).
          05 WS-LONG-MUTUO                      PIC S9(4).
          05 WS-EDT-CONTADORES                  PIC ZZZ.ZZZ.ZZ9.
      *----------------------------------------------------------------*
      *        T A B L A S                                             *
      *----------------------------------------------------------------*

      *----------------------------------------------------------------*
      *      C A M P O S    A U X I L I A R E S  DE  E R R O R R       *
      *----------------------------------------------------------------*
       01  MENSAJES.
           05 MEN-OK                      PIC X(31)
           VALUE 'PROCESO FINALIZO CORRECTAMENTE '.
           05 MEN-ERRDAT                  PIC X(23)
           VALUE 'DATO INFORMADO ERRONEO '.
           05 MEN-ERRDB2                  PIC X(13)
           VALUE 'ERROR DE DB2 '.
           05 MEN-NOEXI                   PIC X(39)
           VALUE 'NO HAY MUTUO PARA LOS DATOS INFORMADOS '.
           05 MEN-YAEXI                   PIC X(41)
           VALUE 'YA EXISTE MUTUO PARA EL CONTRATO EN BASE '.
           05 MEN-ERRPR                   PIC X(34)
           VALUE 'NO HAY DATOS EN BASE DE PRESTAMOS '.
           05 MEN-ERRCO                   PIC X(34)
           VALUE 'NO HAY DATOS EN BASE DE CONTRATOS '.
           05 MEN-ERRPE                   PIC X(33)
           VALUE 'NO HAY DATOS EN BASE DE CLIENTES '.
           05 MEN-ERRIC                   PIC X(36)
           VALUE 'NO HAY DATOS EN BASE COMPLEMENTARIA '.
           05 MEN-ERRVS                   PIC X(29)
           VALUE 'ERROR EN TRATAMIENTO DE VSAM '.
      *-> CAMPOS AUXILIARES DE ERROR
          05 WS-ERR-TIPO               PIC X(02)  VALUE SPACES.
          05 WS-ERR-ACCION             PIC X(10)  VALUE SPACES.
          05 WS-ERR-ELEMENTO           PIC X(08)  VALUE SPACES.
          05 WS-ERR-PROGRAMA           PIC X(08)  VALUE SPACES.
          05 WS-ERR-CODIGO             PIC 9(05)  VALUE ZEROES.
          05 WS-ERR-STATUS             PIC X(02)  VALUE SPACES.
          05 WS-ERR-DATOS-IO           PIC X(20)  VALUE SPACES.
          05 WS-ERR-PARRAFO            PIC X(30)  VALUE SPACES.
          05 WS-ERR-MENSAJE            PIC X(30)  VALUE SPACES.
      *
      *----------------------------------------------------------------*
      *              C O N T A D O R E S                               *
      *----------------------------------------------------------------*
       01 CONTADORES.
          03 WS-LEIDOS-ENTRADA            PIC 9(10).
          03 WS-GRABADOS-SALIDA           PIC 9(10).
          03 WS-GRABADOS-DESCAR           PIC 9(10).
          03 WS-LEIDOS-FECLIQ             PIC 9(10).
          03 WS-IN-T2226OE0               PIC 9(10).
          03 WS-IN-T2227MP0               PIC 9(10).
          03 WS-IN-T2025PR0               PIC 9(10).
      *----------------------------------------------------------------*
      *                    FECHAS                                      *
      *----------------------------------------------------------------*

       01 WS-FECHAS.
      *
          05 WS-FECHA-SYS.
             10 WS-YY              PIC 9999.
             10 WS-MM              PIC 99.
             10 WS-DD              PIC 99.
      *
          05 WS-FECHA-SQL.
             10 WS-ANIO            PIC 9(4).
             10 FILLER             PIC X VALUE '-'.
             10 WS-MES             PIC 99.
             10 FILLER             PIC X VALUE '-'.
             10 WS-DIA             PIC 99.

      *----------------------------------------------------------------*
      *                    SWITCHES - INDICADORES                      *
      *----------------------------------------------------------------*
       01 SWITCHES.
          03 WS-SQLCODE                   PIC S9(04) VALUE ZEROES.
             88  WS-SQLCODE-OK                       VALUE ZEROES.
             88  WS-SQLCODE-NOTF                     VALUE +100.
             88  WS-SQLCODE-DUPKEY                   VALUE -803.
             88  WS-SQLCODE-MORE                     VALUE -811.
          03 WS-ACCION-PROC               PIC X(01)  VALUE 'N'.
             88  WS-ACCION-PROC-OK                   VALUE 'S'.

       01 FS-ENTRADA PIC 99.
           88 FS-E1-OK                    VALUE 00.
           88 FS-E1-FIN                   VALUE 10.
       01 FS-FECLIQ PIC 99.
           88 FS-FC-OK                    VALUE 00.
           88 FS-FC-FIN                   VALUE 10.
       01 FS-SALIDA PIC 99.
           88 FS-S1-OK                    VALUE 00.
       01 FS-DESCAR PIC 99.
           88 FS-S2-OK                    VALUE 00.
      *----------------------------------------------------------------*
      *            S Q L                                               *
      *----------------------------------------------------------------*
           EXEC SQL INCLUDE SQLCA END-EXEC.
           EXEC SQL INCLUDE V2025PR0 END-EXEC.
           EXEC SQL INCLUDE P2025PR0 END-EXEC.
           EXEC SQL INCLUDE V2025MD0 END-EXEC.
           EXEC SQL INCLUDE P2025MD0 END-EXEC.
      *----------------------------------------------------------------*
      *            C O P Y S    E N T R A D A / S A L I D A            *
      *----------------------------------------------------------------*

           COPY PRC05572.
           COPY PTRPOLFE.

      ******************************************************************
      *                                                                *
      *              P R O C E D U R E   D I V I S I O N               *
      *                                                                *
      ******************************************************************
       PROCEDURE DIVISION.

           PERFORM 1000-INICIO
               THRU 1000-INICIO-FIN

           DISPLAY "volviste para Proceso"
           PERFORM 2000-PROCESO
               THRU 2000-PROCESO-FIN
                    UNTIL FS-E1-FIN.

           DISPLAY "volviste para FIN"
           PERFORM 3000-FIN
               THRU 3000-FIN-FIN
           .

      ******************************************************************
       1000-INICIO.
      ******************************************************************

           INITIALIZE CONTADORES
                      SWITCHES.

           PERFORM 1100-ABRIR-ARCHIVOS
              THRU 1100-ABRIR-ARCHIVOS-FIN

           PERFORM 1200-LEER-FECLIQ
              THRU 1200-LEER-FECLIQ-FIN

           PERFORM 1300-LEER-ENTRADA
              THRU 1300-LEER-ENTRADA-FIN

           .
       1000-INICIO-FIN.
           EXIT.
      *----------------------------------------------------------------*
       1100-ABRIR-ARCHIVOS.
      *----------------------------------------------------------------*
           OPEN INPUT FECLIQ
           IF FS-FC-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'OPEN'              TO WS-ERR-ACCION
              MOVE 'FECLIQ'            TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-FECLIQ           TO WS-ERR-STATUS
              MOVE 'ERROR AL ABRIR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '1100-ABRIR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           OPEN INPUT ENTRADA
           IF FS-E1-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'OPEN'              TO WS-ERR-ACCION
              MOVE 'ENTRADA'           TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-ENTRADA          TO WS-ERR-STATUS
              MOVE 'ERROR AL ABRIR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '1100-ABRIR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           OPEN OUTPUT SALIDA
           IF FS-S1-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'OPEN'              TO WS-ERR-ACCION
              MOVE 'SALIDA'            TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-SALIDA           TO WS-ERR-STATUS
              MOVE 'ERROR AL ABRIR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '1100-ABRIR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           OPEN OUTPUT DESCAR
           IF FS-S2-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'OPEN'              TO WS-ERR-ACCION
              MOVE 'DESCAR'            TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-DESCAR           TO WS-ERR-STATUS
              MOVE 'ERROR AL ABRIR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '1100-ABRIR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
           .
      *
       1100-ABRIR-ARCHIVOS-FIN.
           EXIT.
      *
      *----------------------------------------------------------------*
       1200-LEER-FECLIQ.
      *----------------------------------------------------------------*
      *
           READ FECLIQ.
      *
           IF FS-FC-OK
              INITIALIZE PTRPOLFE
              MOVE R-FECLIQ           TO PTRPOLFE
              ADD 1                     TO WS-LEIDOS-FECLIQ
           ELSE
              MOVE MEN-ERRDAT           TO WS-ERR-TIPO
              MOVE 'LEER'               TO WS-ERR-ACCION
              MOVE 'FECLIQ'             TO WS-ERR-ELEMENTO
              MOVE SPACES               TO WS-ERR-DATOS-IO
              MOVE FS-FECLIQ            TO WS-ERR-STATUS
              MOVE 'ERROR AL LEER ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '1200-LEER-FECLIQ' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           MOVE FEC-PAR-PTRPOLFE    TO WS-FECHA-SYS
           MOVE WS-YY               TO WS-ANIO
           MOVE WS-MM               TO WS-MES
           MOVE WS-DD               TO WS-DIA

           DISPLAY 'FECHA: '        WS-FECHA-SQL
           .
       1200-LEER-FECLIQ-FIN.
           EXIT.
      *
      *----------------------------------------------------------------*
       1300-LEER-ENTRADA.
      *----------------------------------------------------------------*
      *
           READ ENTRADA.
      *
           IF FS-E1-OK
              INITIALIZE V2025MD0-P
              MOVE R-ENTRADA            TO V2025MD0-P
              ADD 1                     TO WS-LEIDOS-ENTRADA
           ELSE
              IF FS-E1-FIN
                 IF WS-LEIDOS-ENTRADA = 0
                    DISPLAY 'ARCHIVO DE ENTRADA VACIO'
                 END-IF
              ELSE
                 MOVE MEN-ERRDAT          TO WS-ERR-TIPO
                 MOVE 'LEER'              TO WS-ERR-ACCION
                 MOVE 'ENTRADA'           TO WS-ERR-ELEMENTO
                 MOVE SPACES              TO WS-ERR-DATOS-IO
                 MOVE FS-ENTRADA          TO WS-ERR-STATUS
                 MOVE 'ERROR AL LEER ARCHIVO' TO WS-ERR-MENSAJE
                 MOVE '1300-LEER-ENTRADA' TO WS-ERR-PARRAFO
                 PERFORM 9000-TRATAR-ERROR
              END-IF
           END-IF
           .
       1300-LEER-ENTRADA-FIN.
           EXIT.
      *
      *----------------------------------------------------------------*
       2000-PROCESO.
      *----------------------------------------------------------------*
           PERFORM 2100-OBTENER-DATOS
              THRU 2100-OBTENER-DATOS-FIN
      *
           PERFORM 2200-VERIFICAR-FECHA
              THRU 2200-VERIFICAR-FECHA-FIN
      *
           IF WS-DESCARTAR NOT EQUAL 'SI'
              PERFORM 2300-GRABAR-SALIDA
                 THRU 2300-GRABAR-SALIDA-FIN
      *
              PERFORM 2400-ACTUALIZAR-T2025MD
                 THRU 2400-ACTUALIZAR-T2025MD-FIN
           END-IF
      *
           PERFORM 1300-LEER-ENTRADA
              THRU 1300-LEER-ENTRADA-FIN
           .
       2000-PROCESO-FIN.
           EXIT.
      *
      ******************************************************************
       2100-OBTENER-DATOS.
      ******************************************************************
           DISPLAY 'ESTAS EN OBTENER DATOS'
           INITIALIZE V2025PR0-P
           MOVE V2025MD0-N-ITN-CONT TO V2025PR0-N-ITN-CONT
           EXEC SQL
               SELECT N_ITN_CONT,
                      C_SIT_CONT
                 INTO :V2025PR0-N-ITN-CONT,
                      :V2025PR0-C-SIT-CONT
                 FROM V2025PR0
                WHERE N_ITN_CONT = :V2025PR0-N-ITN-CONT
           END-EXEC
      *
           MOVE SQLCODE TO WS-SQLCODE
                           WS-SQLCODZ
           EVALUATE TRUE
             WHEN WS-SQLCODE-OK
                CONTINUE
             WHEN OTHER
                 MOVE MEN-ERRDB2          TO WS-ERR-TIPO
                 MOVE 'SELECT'            TO WS-ERR-ACCION
                 MOVE 'DB2    '           TO WS-ERR-ELEMENTO
                 MOVE SPACES              TO WS-ERR-DATOS-IO
                 MOVE WS-SQLCODZ          TO WS-ERR-STATUS
                 MOVE 'ERROR AL OBTENER DATOS' TO WS-ERR-MENSAJE
                 MOVE '2100-OBTENER-DATOS' TO WS-ERR-PARRAFO
                 PERFORM 9000-TRATAR-ERROR
           END-EVALUATE
           .
       2100-OBTENER-DATOS-FIN.
           EXIT.
      *----------------------------------------------------------------*
       2200-VERIFICAR-FECHA.
      *----------------------------------------------------------------*
           IF V2025MD0-F-FORM-CON = WS-FECHA-SQL
      *
              PERFORM 2210-VERI-PR-CANCE
                 THRU 2210-VERI-PR-CANCE-FIN
      *
           ELSE
              MOVE V2025MD0-P               TO R-DESCAR
              PERFORM 2900-GRABAR-DESCAR
                 THRU 2900-GRABAR-DESCAR-FIN
              MOVE 'SI'                     TO WS-DESCARTAR
           END-IF
      *
           .
       2200-VERIFICAR-FECHA-FIN.
           EXIT.
      *
      *----------------------------------------------------------------*
       2210-VERI-PR-CANCE.
      *----------------------------------------------------------------*
           IF V2025PR0-C-SIT-CONT NOT EQUAL 4
      *
              PERFORM 2220-VERIFICAR-LONG
                 THRU 2220-VERIFICAR-LONG-FIN
      *
           ELSE
              PERFORM 2900-GRABAR-DESCAR
                 THRU 2900-GRABAR-DESCAR-FIN
              MOVE 'SI'                     TO WS-DESCARTAR
           END-IF
      *
           .
       2210-VERI-PR-CANCE-FIN.
           EXIT.
      *----------------------------------------------------------------*
       2220-VERIFICAR-LONG.
      *----------------------------------------------------------------*
           MOVE LENGTH OF PRC05572-REG TO WS-LONG-MUTUO

           IF V2025MD0-REG-MUTUO-LEN EQUAL WS-LONG-MUTUO
              MOVE 'NO'                     TO WS-DESCARTAR
           ELSE
              PERFORM 2900-GRABAR-DESCAR
                 THRU 2900-GRABAR-DESCAR-FIN
              MOVE 'SI'                     TO WS-DESCARTAR
           END-IF
      *
           .
       2220-VERIFICAR-LONG-FIN.
           EXIT.
      *----------------------------------------------------------------*
       2300-GRABAR-SALIDA.
      *----------------------------------------------------------------*

           MOVE V2025MD0-REG-MUTUO-TEXT TO PRC05572-REG
      *
           WRITE R-SALIDA   FROM PRC05572-REG

           EVALUATE FS-SALIDA
             WHEN '00'
               ADD 1                    TO WS-GRABADOS-SALIDA
             WHEN OTHER
               MOVE MEN-ERRDAT          TO WS-ERR-TIPO
               MOVE 'WRITE'             TO WS-ERR-ACCION
               MOVE 'SALIDA'            TO WS-ERR-ELEMENTO
               MOVE SPACES              TO WS-ERR-DATOS-IO
               MOVE FS-SALIDA           TO WS-ERR-STATUS
               MOVE 'ERROR AL ESCRIBIR ARCHIVO' TO WS-ERR-MENSAJE
               MOVE '2300-GRABAR-SALIDA'        TO WS-ERR-PARRAFO
               PERFORM 9000-TRATAR-ERROR
           END-EVALUATE
           .
       2300-GRABAR-SALIDA-FIN.
           EXIT.
      *
      *----------------------------------------------------------------*
       2900-GRABAR-DESCAR.
      *----------------------------------------------------------------*
           WRITE R-DESCAR   FROM V2025MD0-P

           EVALUATE FS-SALIDA
             WHEN '00'
               ADD 1                    TO WS-GRABADOS-DESCAR
             WHEN OTHER
               MOVE MEN-ERRDAT          TO WS-ERR-TIPO
               MOVE 'WRITE'             TO WS-ERR-ACCION
               MOVE 'SALIDA'            TO WS-ERR-ELEMENTO
               MOVE SPACES              TO WS-ERR-DATOS-IO
               MOVE FS-SALIDA           TO WS-ERR-STATUS
               MOVE 'ERROR AL ESCRIBIR ARCHIVO' TO WS-ERR-MENSAJE
               MOVE '2900-GRABAR-DESCAR'        TO WS-ERR-PARRAFO
               PERFORM 9000-TRATAR-ERROR
           END-EVALUATE
           .
       2900-GRABAR-DESCAR-FIN.
           EXIT.
      *
      ******************************************************************
       2400-ACTUALIZAR-T2025MD.
      ******************************************************************
           MOVE WS-FECHA-SQL TO V2025MD0-FECHA-ACT
           EXEC SQL
                 UPDATE V2025MD0
                   SET ESTA_TRAMA = 2 ,
                       FECHA_ACT  = :V2025MD0-FECHA-ACT
                 WHERE N_ITN_CONT = :V2025MD0-N-ITN-CONT
           END-EXEC.
           MOVE SQLCODE TO WS-SQLCODE
                           WS-SQLCODZ
           EVALUATE TRUE
              WHEN WS-SQLCODE-OK
                 CONTINUE
              WHEN OTHER
                 MOVE MEN-ERRDB2          TO WS-ERR-TIPO
                 MOVE 'UPDATE'            TO WS-ERR-ACCION
                 MOVE 'V2025MD'           TO WS-ERR-ELEMENTO
                 MOVE V2025PR0-N-ITN-CONT TO WS-ERR-DATOS-IO
                 MOVE WS-SQLCODZ          TO WS-ERR-STATUS
                 MOVE 'ERROR UPDATE V2025MD' TO WS-ERR-MENSAJE
                 MOVE '2100-OBTENER-DATOS' TO WS-ERR-PARRAFO
                 PERFORM 9000-TRATAR-ERROR
           END-EVALUATE
           .
       2400-ACTUALIZAR-T2025MD-FIN.
           EXIT.
      ******************************************************************
       3000-FIN.
      ******************************************************************

           PERFORM 3100-CERRAR-ARCHIVOS
              THRU 3100-CERRAR-ARCHIVOS-FIN

           PERFORM 3200-ESTADISTICAS
              THRU 3200-ESTADISTICAS-FIN

           PERFORM 9999-STOP-RUN
              THRU 9999-STOP-RUN-FIN
           .
       3000-FIN-FIN.
           EXIT.
      *
      *----------------------------------------------------------------*
       3100-CERRAR-ARCHIVOS.
      *----------------------------------------------------------------*
           CLOSE FECLIQ
      *
           IF FS-FC-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'CLOSE'             TO WS-ERR-ACCION
              MOVE 'FECLIQ'            TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-FECLIQ           TO WS-ERR-STATUS
              MOVE 'ERROR AL CERRAR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '3100-CERRAR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           CLOSE ENTRADA
      *
           IF FS-E1-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'CLOSE'             TO WS-ERR-ACCION
              MOVE 'ENTRADA'           TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-ENTRADA          TO WS-ERR-STATUS
              MOVE 'ERROR AL CERRAR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '3100-CERRAR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           CLOSE SALIDA
      *
           IF FS-S1-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'CLOSE'             TO WS-ERR-ACCION
              MOVE 'SALIDA'            TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-SALIDA           TO WS-ERR-STATUS
              MOVE 'ERROR AL CERRAR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '3100-CERRAR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
      *
           CLOSE DESCAR
      *
           IF FS-S2-OK
              CONTINUE
           ELSE
              MOVE MEN-ERRDAT          TO WS-ERR-TIPO
              MOVE 'CLOSE'             TO WS-ERR-ACCION
              MOVE 'DESCAR'            TO WS-ERR-ELEMENTO
              MOVE SPACES              TO WS-ERR-DATOS-IO
              MOVE FS-DESCAR           TO WS-ERR-STATUS
              MOVE 'ERROR AL CERRAR ARCHIVO' TO WS-ERR-MENSAJE
              MOVE '3100-CERRAR-ARCHIVOS' TO WS-ERR-PARRAFO
              PERFORM 9000-TRATAR-ERROR
           END-IF
           .
       3100-CERRAR-ARCHIVOS-FIN.
           EXIT.
      *
      ******************************************************************
       3200-ESTADISTICAS.
      ******************************************************************
           DISPLAY ' _______________________________________________ '
           DISPLAY '| PROGRAMA: PTPSEGS3 - TRATA CUOTAS DE HABERES  |'
           DISPLAY '| Y TARJETAS                                    |'
           DISPLAY '|_______________________________________________|'
           MOVE WS-LEIDOS-FECLIQ       TO WS-EDT-CONTADORES
           DISPLAY '| LEIDOS FECLIQ.........:  ' WS-EDT-CONTADORES
           MOVE WS-LEIDOS-ENTRADA      TO WS-EDT-CONTADORES
           DISPLAY '| LEIDOS ENTRADA........:  ' WS-EDT-CONTADORES
           MOVE WS-GRABADOS-SALIDA     TO WS-EDT-CONTADORES
           DISPLAY '| GRABADOS SALIDA.......:  ' WS-EDT-CONTADORES
           MOVE WS-GRABADOS-DESCAR     TO WS-EDT-CONTADORES
           DISPLAY '| GRABADOS DESACAR......:  ' WS-EDT-CONTADORES
           .
       3200-ESTADISTICAS-FIN.
           EXIT.
      *
      ******************************************************************
       9000-TRATAR-ERROR.
      ******************************************************************
           CLOSE  FECLIQ
                  ENTRADA
                  SALIDA
                  DESCAR.
      *
           DISPLAY '****************************************'
           DISPLAY '*     ERROR EN PROGRAMA  PTPSEGS3      *'
           DISPLAY '****************************************'
           DISPLAY '*  TIPO      : ' WS-ERR-TIPO
           DISPLAY '*  PARRAFO   : ' WS-ERR-PARRAFO
           DISPLAY '*  STATUS    : ' WS-ERR-STATUS
           DISPLAY '*  CODIGO    : ' WS-ERR-CODIGO
           DISPLAY '*  COMPONENTE: ' WS-ERR-ELEMENTO
           DISPLAY '*  ACCION    : ' WS-ERR-ACCION
           DISPLAY '*  MENSAJE   : ' WS-ERR-MENSAJE
           DISPLAY '*  PROGRAMA  : ' WS-ERR-PROGRAMA
           DISPLAY '*  DATOS-IO  : ' WS-ERR-DATOS-IO
           DISPLAY '****************************************'
      *
           MOVE WS-ERR-TIPO                TO RETURN-CODE
      *
           PERFORM 9999-STOP-RUN
              THRU 9999-STOP-RUN-FIN
           .
       9000-TRATAR-ERROR-FIN.
           EXIT.
      *
      ******************************************************************
       9999-STOP-RUN.
      ******************************************************************
           STOP RUN
           .
       9999-STOP-RUN-FIN.
           EXIT.
      *
      ******************************************************************
      *                   FIN PROGRAMA PTLAUT05                        *
      ******************************************************************
